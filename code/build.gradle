plugins {
    id 'java'
    id 'com.enonic.xp.app' version '3.2.0'
    id "com.github.node-gradle.node" version "3.5.1"
}

sourceCompatibility = "1.11"
targetCompatibility = "1.11"

app {
    name = project.ext.appName
    displayName = project.ext.displayName
    vendorName = project.ext.vendorName
    vendorUrl = project.ext.vendorUrl
    systemVersion = "${xpVersion}"
    keepArchiveFileName = true

    rawDevSourcePaths = ["/enonic-xp/devpath", "/enonic-xp/devpathbuild"]
}

ext {
    deployDir = "../deploy/"
}

sourceSets {
    main {
        resources {
            srcDir "site"
        }
    }
}

task packageApplication {
    dependsOn(':build', 'npmBuild')
    group 'application'
    description 'Packages entire application'
    doLast {
        copy {
            from "build/libs/${projectName}-${version}.jar"
            into "${deployDir}"
            rename ("${projectName}-${version}.jar", "${projectName}.jar")
        }
    }
}

task npmBuild(
        type: NpmTask,
        group: 'node',
        dependsOn: 'npmLint',
        description: 'Build javascript'
) {
    inputs.dir 'src'
    outputs.dir "$buildDir/resources"
    args = ['run', 'build']
}

task npmLint(
        type: NpmTask,
        group: 'node',
        dependsOn: ['installWithCI'],
        description: 'Run lint'
) {
    inputs.dir 'src/main'
    outputs.dir "$buildDir/lint-results"
    args = ['run', 'lint']
}

task installWithCI(
        type: NpmTask,
        group: 'node',
        description: 'Install node dependencies with ci, ignoring scripts'
) {
    inputs.files "package.json"
    outputs.dir "./node_modules"
    args = ['ci', '--ignore-scripts', '--quiet']
}

task npm_install(
        type: NpmTask,
        group: 'node',
        description: 'Install node dependencies, ignoring scripts'
) {
    args = ['install', '--ignore-scripts', '--quiet']
}

task appName {
    group 'application'
    description 'Get the name of the application from gradle.properties'
    doLast {
        print "${projectName}".replaceAll(/[^a-zA-Z]/, "");
    }
}

task enonicVersion {
    group 'application'
    description 'Get the enonic XP version from gradle.properties'
    doLast {
        print "${xpVersion}"
    }
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:testing:${xpVersion}"
    include "com.enonic.lib:lib-cache:2.1.0"
    include "com.enonic.lib:lib-http-client:3.2.1"
    include "com.github.tineikt:xp-lib-freemarker:1.1.6"
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
    maven { url "https://jitpack.io" } // Needed by freemarker
}

node {
    // Version of node to use.
    version = "16.14.2"

    // Version of npm to use.
    npmVersion = "8.5.0"

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

wrapper {
    gradleVersion = "7.3.1"
    distributionType = Wrapper.DistributionType.BIN
}
